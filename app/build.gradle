apply plugin: 'com.android.application'

android {

    compileSdkVersion 29

    defaultConfig {
        applicationId "com.zendesk.rememberthedate"
        minSdkVersion 16
        targetSdkVersion 29

        versionCode 25
        versionName "2.2.3"

        resValue "string", "google_api_key", System.getenv("zdGoogleApiKey")
                ?: rootProject.properties["zdGoogleApiKey"]
                ?: "replace me"
        resValue "string", "fcm_application_id", System.getenv("zdFcmApplicationId")
                ?: rootProject.properties["zdFcmApplicationId"]
                ?: "replace me"
    }

    signingConfigs {
        release {
            storeFile file("../scripts/rtd.jks")
            storePassword System.getenv("RTD_STORE_PASS")
            keyAlias System.getenv("RTD_ALIAS")
            keyPassword System.getenv("RTD_KEY_PASS")
        }
    }

    buildTypes {

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            if (file("../scripts/rtd.jks").exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // Don't change this to false
        abortOnError true
        lintConfig file("lint.xml")
    }
}

dependencies {
    implementation group: 'androidx.legacy', name: 'legacy-support-v4', version: '1.0.0'
    implementation group: 'com.google.android.material', name: 'material', version: '1.2.0'
    implementation group: 'androidx.appcompat', name: 'appcompat', version: '1.1.0'
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.1.0'
    implementation group: 'androidx.cardview', name: 'cardview', version: '1.0.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    implementation group: 'com.google.android.gms', name: 'play-services-gcm', version: '17.0.0'
    implementation group: 'com.google.firebase', name: 'firebase-messaging', version: '20.2.4'

    implementation group: 'com.zendesk', name: 'support', version: '5.0.0'
    implementation group: 'com.zendesk', name: 'messaging', version: '5.0.0'
    implementation group: 'com.zendesk', name: 'answerbot', version: '3.0.0'
    implementation group: 'com.zendesk', name: 'chat', version: '3.0.0'
}